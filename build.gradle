plugins {
	id 'java-library'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'org.cadixdev.licenser' version '0.6.1'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
}

def projectGroup = 'com.clubobsidian'
def projectVersion = '6.1.0'

allprojects {
	group projectGroup
	version projectVersion
	ext {
		junitVersion = '5.9.1'
		cloudVersion = '1.8.0'
		wrappyVersion = "3.0.0"
		langVersion = "3.12.0"
		guiceVersion = "5.1.0"
	}
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'com.github.johnrengelman.shadow'
	apply plugin: 'org.cadixdev.licenser'
	apply plugin: 'maven-publish'

	publishing {
		publications {
			shadow(MavenPublication) {
				groupId projectGroup
				artifactId project.name
				version projectVersion
				artifact shadowJar
			}
		}
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(11)
		}
	}

	license {
		header = project.rootProject.file('HEADER.txt')
		include '**/*.java'
	}

	test {
		useJUnitPlatform()
	}

	shadowJar {
		archiveClassifier.set('')
		/*minimize() {
			exclude(project(":core"))
		}*/
		relocate 'org.spongepowered.configurate', 'com.clubobsidian.dynamicgui.shaded.configurate'
		relocate 'com.google.common', 'com.clubobsidian.dynamicgui.shaded.guava'
		relocate 'com.google.gson', 'com.clubobsidian.dynamicgui.shaded.gson'
		relocate 'org.yaml.snakeyaml', 'com.clubobsidian.dynamicgui.shaded.snakeyaml'
		relocate 'com.clubobsidian.trident', 'com.clubobsidian.dynamicgui.shaded.trident'
		relocate 'com.clubobsidian.wrappy', 'com.clubobsidian.dynamicgui.shaded.wrappy'
		relocate 'net.kyori', 'com.clubobsidian.dynamicgui.shaded.kyori'
		relocate 'com.clubobsidian.fuzzutil', 'com.clubobsidian.dynamicgui.shaded.fuzzutil'
		relocate 'io.leangen', 'com.clubobsidian.dynamicgui.shaded.leangen'
		relocate 'org.codehaus.mojo', 'com.clubobsidian.dynamicgui.shaded.mojo'
		relocate 'org.checkerframework.framework', 'com.clubobsidian.dynamicgui.shaded.checkerframework.framework'
		relocate 'org.checkerframework.common', 'com.clubobsidian.dynamicgui.shaded.checkerframework.common'
		relocate 'org.checkerframework.checker', 'com.clubobsidian.dynamicgui.shaded.checkerframework.checker'
		relocate 'org.checkerframework.dataflow', 'com.clubobsidian.dynamicgui.shaded.checkerframework.dataflow'
		relocate 'org.apache.commons', 'com.clubobsidian.dynamicgui.shaded.apachecommons'
		relocate 'org.aopalliance.intercept', 'com.clubobsidian.dynamicgui.shaded.aopalliance.intercept'
		relocate 'org.aopalliance.aop', 'com.clubobsidian.dynamicgui.shaded.aopalliance.aop'
		relocate 'javax.inject', 'com.clubobsidian.dynamicgui.shaded.javax.inject'
		relocate 'com.udojava.evalex', 'com.clubobsidian.dynamicgui.shaded.evalex'
		relocate 'com.typesafe.config', 'com.clubobsidian.dynamicgui.shaded.typesafeconfig'
		relocate 'com.google.j2objc', 'com.clubobsidian.dynamicgui.shaded.j2objc'
		relocate 'com.google.inject', 'com.clubobsidian.dynamicgui.shaded.guice'
		relocate 'javax.annotation', 'com.clubobsidian.dynamicgui.shaded.javaxannotation'
		relocate 'com.google.errorprone', 'com.clubobsidian.dynamicgui.shaded.errorprone'
		relocate 'com.fasterxml.jackson', 'com.clubobsidian.dynamicgui.shaded.jackson'
		relocate 'com.github.benmanes.caffeine', 'com.clubobsidian.dynamicgui.shaded.caffeine'
		relocate 'cloud', 'com.clubobsidian.dynamicgui.shaded.cloud'
		relocate 'com.google.thirdparty', 'com.clubobsidian.dynamicgui.shaded.google.thirdparty'
		relocate 'io.github.classgraph', 'com.clubobsidian.dynamicgui.shaded.classgraph'
		relocate 'nonapi.io.github.classgraph', 'com.clubobsidian.dynamicgui.shaded.nonapi.io.github.classgraph'
		relocate 'javassist', 'com.clubobsidian.dynamicgui.shaded.javassist'
	}

	repositories {
		maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"}
		maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}

	dependencies {
		testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
		testImplementation 'org.mockito:mockito-core:4.8.0'
		compileOnly("org.jetbrains:annotations:23.0.0")
		compileOnly("org.apache.commons:commons-lang3:$langVersion")
		testImplementation("org.apache.commons:commons-lang3:$langVersion")
	}
}