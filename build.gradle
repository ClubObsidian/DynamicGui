plugins {
	id 'java-library'
	id 'com.gradleup.shadow' version '9.1.0'
	id "dev.yumi.gradle.licenser" version "2.1.1"
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
}

def projectGroup = 'com.clubobsidian'
def projectVersion = '6.3.0'

allprojects {
	group projectGroup
	version projectVersion
	ext {
		junitVersion = '5.13.4'
		cloudVersion = '2.0.0'
		wrappyVersion = "3.0.1"
		langVersion = "3.12.0"
		guiceVersion = "7.0.0"
	}
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'com.gradleup.shadow'
	apply plugin: 'dev.yumi.gradle.licenser'
	apply plugin: 'maven-publish'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
		withJavadocJar()
		withSourcesJar()
	}

	javadoc {
		if(JavaVersion.current().isJava9Compatible()) {
			options.addBooleanOption('html5', true)
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId projectGroup
				artifactId project.name
				version projectVersion
				artifact jar
				artifact javadocJar
				artifact sourcesJar
			}
		}
	}

	license {
		rule project.rootProject.file('HEADER.txt')
		include '**/*.java'
	}

	test {
		useJUnitPlatform()
	}

	shadowJar {
		/*minimize() {
			exclude(project(":core"))
		}*/
		relocate 'org.spongepowered.configurate', 'com.clubobsidian.dynamicgui.shaded.configurate'
		relocate 'com.google.common', 'com.clubobsidian.dynamicgui.shaded.guava'
		relocate 'com.google.gson', 'com.clubobsidian.dynamicgui.shaded.gson'
		relocate 'org.yaml.snakeyaml', 'com.clubobsidian.dynamicgui.shaded.snakeyaml'
		relocate 'com.clubobsidian.trident', 'com.clubobsidian.dynamicgui.shaded.trident'
		relocate 'com.clubobsidian.wrappy', 'com.clubobsidian.dynamicgui.shaded.wrappy'
		relocate 'net.kyori', 'com.clubobsidian.dynamicgui.shaded.kyori'
		relocate 'com.clubobsidian.fuzzutil', 'com.clubobsidian.dynamicgui.shaded.fuzzutil'
		relocate 'io.leangen', 'com.clubobsidian.dynamicgui.shaded.leangen'
		relocate 'org.codehaus.mojo', 'com.clubobsidian.dynamicgui.shaded.mojo'
		relocate 'org.checkerframework.framework', 'com.clubobsidian.dynamicgui.shaded.checkerframework.framework'
		relocate 'org.checkerframework.common', 'com.clubobsidian.dynamicgui.shaded.checkerframework.common'
		relocate 'org.checkerframework.checker', 'com.clubobsidian.dynamicgui.shaded.checkerframework.checker'
		relocate 'org.checkerframework.dataflow', 'com.clubobsidian.dynamicgui.shaded.checkerframework.dataflow'
		relocate 'org.apache.commons', 'com.clubobsidian.dynamicgui.shaded.apachecommons'
		relocate 'org.aopalliance.intercept', 'com.clubobsidian.dynamicgui.shaded.aopalliance.intercept'
		relocate 'org.aopalliance.aop', 'com.clubobsidian.dynamicgui.shaded.aopalliance.aop'
		relocate 'javax.inject', 'com.clubobsidian.dynamicgui.shaded.javax.inject'
		relocate 'com.ezylang.evalex', 'com.clubobsidian.dynamicgui.shaded.evalex'
		relocate 'com.typesafe.config', 'com.clubobsidian.dynamicgui.shaded.typesafeconfig'
		relocate 'com.google.j2objc', 'com.clubobsidian.dynamicgui.shaded.j2objc'
		relocate 'com.google.inject', 'com.clubobsidian.dynamicgui.shaded.guice'
		relocate 'javax.annotation', 'com.clubobsidian.dynamicgui.shaded.javaxannotation'
		relocate 'com.google.errorprone', 'com.clubobsidian.dynamicgui.shaded.errorprone'
		relocate 'com.fasterxml.jackson', 'com.clubobsidian.dynamicgui.shaded.jackson'
		relocate 'com.github.benmanes.caffeine', 'com.clubobsidian.dynamicgui.shaded.caffeine'
		relocate 'org.incendo.cloud', 'com.clubobsidian.dynamicgui.shaded.cloud'
		relocate 'com.google.thirdparty', 'com.clubobsidian.dynamicgui.shaded.google.thirdparty'
		relocate 'io.github.classgraph', 'com.clubobsidian.dynamicgui.shaded.classgraph'
		relocate 'nonapi.io.github.classgraph', 'com.clubobsidian.dynamicgui.shaded.nonapi.io.github.classgraph'
		relocate 'javassist', 'com.clubobsidian.dynamicgui.shaded.javassist'
	}

	repositories {
		maven { url = "https://repo.papermc.io/repository/maven-public/" }
		mavenCentral()
		maven { url = "https://jitpack.io" }
	}

	dependencies {
		testImplementation(platform('org.junit:junit-bom:5.13.4'))
		testImplementation('org.junit.jupiter:junit-jupiter')
		testRuntimeOnly('org.junit.platform:junit-platform-launcher')
		testImplementation 'org.mockito:mockito-core:5.20.0'
		compileOnly("org.incendo:cloud-core:$cloudVersion")
		testRuntimeOnly("org.incendo:cloud-core:$cloudVersion")
		compileOnly("org.jetbrains:annotations:23.0.0")
		compileOnly("org.apache.commons:commons-lang3:$langVersion")
		testImplementation("org.apache.commons:commons-lang3:$langVersion")
	}
}